#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.амед_КонтрольНеявокПациентовНаПО);
	РасписаниеРегламентногоЗадания = РегламентноеЗадание.Расписание;
	
	// Чтение параметров
	ПрочитатьНастройки();

	// Установки видимости элементов
	УправлениеЭлементамиФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрытьНаКлиенте", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ОтправитьОтчет Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("УчетныеЗаписиЭлектроннойПочты");
		МассивНепроверяемыхРеквизитов.Добавить("ПолучателиПисьма");
		
	КонецЕсли;	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы


&НаКлиенте
Процедура ПолучателиПисьмаПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора",, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПисьмаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	
	//ТекущиеДанные.Представление = ВзаимодействияКлиентСервер.ПолучитьПредставлениеАдресата(ВыбранноеЗначение.Наименование, ВыбранноеЗначение.Адрес, "");
	
КонецПроцедуры

&НаКлиенте
Процедура УчетныеЗаписиЭлектроннойПочтыПриИзменении(Элемент)
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчетОВыполненииЗаданияПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы(ЭтаФорма);

КонецПроцедуры


#КонецОбласти
	

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиенте() Экспорт
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	СохранитьНастройки();
	
	// Сброс флага
	СброситьФлагМодифицированности();
	
	Закрыть();
	
КонецПроцедуры	

&НаКлиенте
Процедура СброситьФлагМодифицированности()
	
	Модифицированность = Ложь;
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаПолучателиПисьма.ТолькоПросмотр = Не(Форма.ОтправитьОтчет);
 	
	Элементы.УчетныеЗаписиЭлектроннойПочты.АвтоОтметкаНезаполненного = Форма.ОтправитьОтчет;
	Элементы.УчетныеЗаписиЭлектроннойПочты.ОтметкаНезаполненного = Форма.ОтправитьОтчет И Не ЗначениеЗаполнено(Форма.УчетныеЗаписиЭлектроннойПочты);
	
	Элементы.ПолучателиПисьма.АвтоОтметкаНезаполненного = Форма.ОтправитьОтчет;
	Элементы.ПолучателиПисьма.ОтметкаНезаполненного = Форма.ОтправитьОтчет И (Форма.ПолучателиПисьма.Количество() = 0);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройки()

	ПараметрыЗадания = амед_КонтрольНеявокПациентовНаПрофосмотр.ПолучитьСтруктуруПараметровЗадания();
	ПараметрыВХранилище = Константы.амед_КонтрольНеявокНаПрофосмотр.Получить().Получить();
	Если ЗначениеЗаполнено(ПараметрыВХранилище) Тогда
		
		ЗаполнитьЗначенияСвойств(
			ПараметрыЗадания, ПараметрыВХранилище
		);	

	КонецЕсли;	
	
	Номенклатура.ЗагрузитьЗначения(ПараметрыЗадания.Номенклатура);
	Пользователи.ЗагрузитьЗначения(ПараметрыЗадания.Пользователи);
	
	Для Каждого Строка Из ПараметрыЗадания.ПолучателиПисьма Цикл
		
		ЗаполнитьЗначенияСвойств(
			ПолучателиПисьма.Добавить(),
			Строка
		);	
		
	КонецЦикла;
	
КонецПроцедуры	
	
&НаСервере	
Процедура СохранитьНастройки()
	
	// Заполнение параметров 
	ЗаполнитьЗначенияСвойств(
		ПараметрыЗадания,
		ЭтаФорма,
		"ОтправитьОтчет, УчетныеЗаписиЭлектроннойПочты"
	);
	
	// Прочие настройки
	ПараметрыЗадания.Номенклатура = Номенклатура.ВыгрузитьЗначения();
	ПараметрыЗадания.Пользователи = Пользователи.ВыгрузитьЗначения();
	
	Значение = РеквизитФормыВЗначение("ПолучателиПисьма");
	Для Каждого Строка Из Значение Цикл
		
		ПараметрыЗадания.ПолучателиПисьма.Добавить(
			ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Строка)
		);
		
	КонецЦикла;
	
	// 
	Константы.амед_КонтрольНеявокНаПрофосмотр.Установить(
		Новый ХранилищеЗначения(ПараметрыЗадания)
	)	
	
КонецПроцедуры	

#КонецОбласти
